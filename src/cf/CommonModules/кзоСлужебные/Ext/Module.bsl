
#Область ПрограммныйИнтерфейс

// Возвращает дату объекта, на которую необходимо производить операции заполнения или проверки
//
// Параметры:
//  Объект	 - ДокументОбъект, СправочникОбъект, ОбъектФормыКлиентскогоПриложения	 - Любой объект имеющий реквизит Ссылка
// 
// Возвращаемое значение:
//  Дата - Дата для проведения операций
//
Функция ПолучитьДатуОбъекта(Объект) Экспорт

	Дата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	Если ОбщегоНазначения.ВидОбъектаПоСсылке(Объект.Ссылка) = "Документ" 
		И ЗначениеЗаполнено(Объект.Дата)
		Тогда
		Дата = Объект.Дата;	
	КонецЕсли;
	
	Возврат Дата;
	
КонецФункции

// Возвращает описание типов объектов для заполнения
// 
// Возвращаемое значение:
//  ОписаниеТипов - Описание типов 
//
Функция ПолучитьТипыОбъектовДляЗаполнения() Экспорт
	
	МассивТипов = Метаданные.ОпределяемыеТипы.кзоОбъектыДляЗаполнения.Тип.Типы();
    МассивТипов = ПреобразоватьМассивТиповОбъектовВМассивТиповСсылок(МассивТипов);
	
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	Возврат ОписаниеТипов;
	
КонецФункции

// Возвращает описание типов объектов для проверки заполнения
// 
// Возвращаемое значение:
//  ОписаниеТипов - Описание типов 
//
Функция ПолучитьТипыОбъектовПроверкиЗаполнения() Экспорт
	
	МассивТипов = Метаданные.ОпределяемыеТипы.кзоОбъектыПроверкиЗаполнения.Тип.Типы();
    МассивТипов = ПреобразоватьМассивТиповОбъектовВМассивТиповСсылок(МассивТипов);
	
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	Возврат ОписаниеТипов;
	
КонецФункции

// Возвращает описание типов объектов событий форм
// 
// Возвращаемое значение:
//  ОписаниеТипов - Описание типов 
//
Функция ПолучитьТипыОбъектовСобытийФорм() Экспорт
	
	МассивТипов = Метаданные.ОпределяемыеТипы.кзоОбъектыСобытийФорм.Тип.Типы();
	МассивТипов = ПреобразоватьМассивТиповОбъектовВМассивТиповСсылок(МассивТипов);
	
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	
	Возврат ОписаниеТипов;
    
КонецФункции

// Возвращает строку представления списков пользователей и групп
//
// Параметры:
//  Правило	 - ДокументОбъект.кзоПравилаЗаполнения, ДокументОбъект.кзоПравилаПроверкиЗаполнения	 - Правило
// 
// Возвращаемое значение:
//  Строка - Строка представления
//
Функция ПолучитьПредставлениеПользователейИГрупп(Правило) Экспорт
	
	Массив = Новый Массив;
	Для каждого Строка Из Правило.ГруппыПользователей Цикл
		Массив.Добавить(Строка(Строка.Группа));	
	КонецЦикла;
	Для каждого Строка Из Правило.Пользователи Цикл
		Массив.Добавить(Строка(Строка.Пользователь));	
	КонецЦикла;
	
	Массив = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Массив);
	
	Возврат СтрСоединить(Массив, ", ");
		
КонецФункции

// Преобразует массив типов объектов (например "ДокументОбъект.Заказ") в массив типов ссылок (например "ДокументСсылка.Заказ")
//
// Параметры:
//  МассивТиповОбъектов	 - Массив	 - массив типов объектов, например "ДокументОбъект.Заказ"
// 
// Возвращаемое значение:
//  Массив - массив типов ссылок, например "ДокументСсылка.Заказ"
//
Функция ПреобразоватьМассивТиповОбъектовВМассивТиповСсылок(МассивТиповОбъектов) Экспорт
	
	МассивТиповСсылок = Новый Массив;
	
	Для Каждого ТекЭлемент Из МассивТиповОбъектов Цикл 
		
		Попытка			
			НовыйОбъект = Новый (ТекЭлемент);			
			МассивТиповСсылок.Добавить(ТипЗнч(НовыйОбъект.Ссылка));
			
		Исключение			
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат МассивТиповСсылок;
		
КонецФункции

#КонецОбласти


