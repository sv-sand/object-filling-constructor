 
#Область Служебные

&НаСервере
Процедура УправлениеЭлементами()
	
	Если Объект.Режим = "Загрузка" Тогда
		Заголовок = "Загрузка правил (кзо)";
		Элементы.ФормаРежимЗагрузка.Пометка = Истина;
		Элементы.ФормаРежимВыгрузка.Пометка = Ложь;
		
		Элементы.ФормаЗагрузить.Видимость = Истина;
		Элементы.ФормаВыгрузить.Видимость = Ложь;
		Элементы.ЗагружатьНеАктивными.Видимость = Истина;
		
		Элементы.ПравилаКВыгрузкеДобавить.Видимость = Ложь;
		Элементы.ПравилаКВыгрузкеИзменить.Видимость = Ложь;
		Элементы.ПравилаКВыгрузкеУдалить.Видимость = Ложь;
		Элементы.ПравилаКВыгрузкеЗаполнить.Видимость = Ложь; 
		
		Элементы.ПравилаКВыгрузкеПравило.ТолькоПросмотр = Истина;
		
	ИначеЕсли Объект.Режим = "Выгрузка" Тогда	
		Заголовок = "Выгрузка правил (кзо)";
		Элементы.ФормаРежимЗагрузка.Пометка = Ложь;
		Элементы.ФормаРежимВыгрузка.Пометка = Истина;
		
		Элементы.ФормаЗагрузить.Видимость = Ложь;
		Элементы.ФормаВыгрузить.Видимость = Истина;
		Элементы.ЗагружатьНеАктивными.Видимость = Ложь;
		
		Элементы.ПравилаКВыгрузкеДобавить.Видимость = Истина;
		Элементы.ПравилаКВыгрузкеИзменить.Видимость = Истина;
		Элементы.ПравилаКВыгрузкеУдалить.Видимость = Истина;
		Элементы.ПравилаКВыгрузкеЗаполнить.Видимость = Истина;
		
		Элементы.ПравилаКВыгрузкеПравило.ТолькоПросмотр = Ложь;
	Иначе
		ВызватьИсключение "Не верно установлен режим работы обработки";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПравила(Правила)
	
	Для каждого Строка Из Правила Цикл
		НоваяСтрока = Объект.Правила.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.ТипЗначения = ТипЗнч(НоваяСтрока.Правило);
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти
 
#Область Команды

&НаКлиенте
Процедура РежимВыгрузка(Команда)
	
	Объект.Правила.Очистить();
	Объект.Режим = "Выгрузка";
	УправлениеЭлементами();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимЗагрузка(Команда)
	
	Объект.Правила.Очистить();
	Объект.Режим = "Загрузка";
	УправлениеЭлементами();
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();		
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.Правила.Очистить();
	
	МассивСсылок = Обработки.кзоВыгрузкаЗагрузкаПравил.ПолучитьВсеПравила();
	Для каждого Правило Из МассивСсылок Цикл
		НоваяСтрока = Объект.Правила.Добавить();	
		НоваяСтрока.Правило = Правило;
		НоваяСтрока.ТипЗначения = ТипЗнч(НоваяСтрока.Правило);
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ИмяФайла) Тогда
		ПоказатьПредупреждение(, "Не заполнено имя файла", 10, "ВНИМАНИЕ!");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Правила.Количество() = 0) Тогда
		ПоказатьПредупреждение(, "Нет правил к выгрузке", 10, "ВНИМАНИЕ!");
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанные = ВыгрузитьНаСервере();
	ДвоичныеДанные.Записать(Объект.ИмяФайла);
	
	ПоказатьОповещениеПользователя("Выгрузка выполнена", , , , , КлючУникальности);
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьНаСервере()
	
	МассивСсылок = Объект.Правила.Выгрузить().ВыгрузитьКолонку("Правило");
	Возврат Обработки.кзоВыгрузкаЗагрузкаПравил.ВыгрузитьВДвоичныеДанные(МассивСсылок);
		
КонецФункции

&НаКлиенте
Процедура Загрузить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ИмяФайла) Тогда
		ПоказатьПредупреждение(, "Не заполнено имя файла", 10, "ВНИМАНИЕ!");
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанные = Новый ДвоичныеДанные(Объект.ИмяФайла);
	ЗагрузитьНаСервере(ДвоичныеДанные);
	
	Оповестить("кзоПравилаЗаполнения.ОбновитьСписок");
	Оповестить("кзоПравилаПроверкиЗаполнения.ОбновитьСписок");
	Оповестить("кзоСобытияЭлементовФорм.ОбновитьСписок");
	
	ПоказатьОповещениеПользователя("Загрузка выполнена", , , , , КлючУникальности);
		
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере(ДвоичныеДанные)
	
	Объект.Правила.Очистить();
	
	МассивСсылок = Обработки.кзоВыгрузкаЗагрузкаПравил.ЗагрузитьИзДвоичныхДанных(ДвоичныеДанные, Объект.ЗагружатьНеАктивными);
	Для каждого Правило Из МассивСсылок Цикл
		НоваяСтрока = Объект.Правила.Добавить();	
		НоваяСтрока.Правило = Правило;
		НоваяСтрока.ТипЗначения = ТипЗнч(НоваяСтрока.Правило);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Режим", Объект.Режим);
	Если НЕ ЗначениеЗаполнено(Объект.Режим) Тогда
		Объект.Режим = "Выгрузка";
	КонецЕсли;
	
	Если Параметры.Свойство("Правила") Тогда
		ЗаполнитьПравила(Параметры.Правила);
	КонецЕсли;
	
	УправлениеЭлементами();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ИмяФайлаНачалоВыбораПродолжение", ЭтотОбъект);
	
	Если Объект.Режим = "Выгрузка" Тогда
		РежимРаботы = РежимДиалогаВыбораФайла.Сохранение;
	ИначеЕсли Объект.Режим = "Загрузка" Тогда	
		РежимРаботы = РежимДиалогаВыбораФайла.Открытие;
	Иначе
		ВызватьИсключение "Не верно установлен режим работы обработки";
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимРаботы);
	Диалог.Заголовок = "Выберите файлы";
	Диалог.ПолноеИмяФайла = "Правила заполнения и проверки.xml";
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = НСтр("ru = 'Файл выгрузки'") + "(*.xml)|*.xml";
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНачалоВыбораПродолжение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ИмяФайла Из ВыбранныеФайлы Цикл
		Объект.ИмяФайла = ИмяФайла;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПравилаКВыгрузкеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Объект.Режим = "Выгрузка" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "ПравилаКВыгрузкеПравило" Тогда
		Строка = Объект.Правила.НайтиПоИдентификатору(ВыбраннаяСтрока);
		ПоказатьЗначение(, Строка.Правило);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
