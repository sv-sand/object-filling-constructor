
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли;
	
	ПроверитьДубликаты(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область Служебные

Процедура ПроверитьДубликаты(Отказ)

	Если ЭтоГруппа Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СобытияЭлементовФорм.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.кзоСобытияЭлементовФорм КАК СобытияЭлементовФорм
		|ГДЕ
		|	СобытияЭлементовФорм.Ссылка <> &Ссылка
		|	И СобытияЭлементовФорм.ОбъектМетаданных = &ОбъектМетаданных
		|	И СобытияЭлементовФорм.ИмяФормы = &ИмяФормы
		|	И СобытияЭлементовФорм.ИмяЭлемента = &ИмяЭлемента
		|	И СобытияЭлементовФорм.ИмяСобытия = &ИмяСобытия
		|	И НЕ СобытияЭлементовФорм.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	Запрос.УстановитьПараметр("ИмяФормы", ИмяФормы);
	Запрос.УстановитьПараметр("ИмяЭлемента", ИмяЭлемента);
	Запрос.УстановитьПараметр("ИмяСобытия", ИмяСобытия);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Текст = СтрШаблон(
		"Найдено событие элемента формы ""%1"" с совпадающими реквизитами:
		|%2, %3, %4, %5",
		Выборка.Ссылка,
		ОбъектМетаданных,
		ИмяФормы,
		ИмяЭлемента,
		ИмяСобытия
	);
	
	ОбщегоНазначения.СообщитьПользователю(Текст, Ссылка, , , Отказ);
	
КонецПроцедуры

#КонецОбласти
