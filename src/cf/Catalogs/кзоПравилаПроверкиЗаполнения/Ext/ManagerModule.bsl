
#Область ПрограммныйИнтерфейс

// Возвращает наименование элемента справочника
//
// Параметры:
//  Объект	 - СправочникОбъект.кзоПравилаПроверкиЗаполнения - Справочник объект
//  	- ОбъектФормы - Объект формы
// 
// Возвращаемое значение:
//  Строка - Наименование
//
Функция СформироватьНаименование(Объект) Экспорт
	
	Текст = Строка(Объект.ОбъектМетаданных);
	
	Если ЗначениеЗаполнено(Объект.ИмяТаблицы) Тогда
		Текст = Текст + " / " + Объект.ИмяТаблицы;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Конвертирует правила заполнения из документа кзоПравилаЗаполнения в отбор
//
// Параметры:
//  Документ - ДокументСсылка.кзоПравилаЗаполнения	 - Документ
// 
// Возвращаемое значение:
//  Массив - Массив структур, контейнер представляющий собой сериализованный отбор
//
Функция ЗаполнениеИзПравилВОтбор(Документ) Экспорт
	
	НастройкиПроверки = Новый Массив;
	
	Для каждого Строка Из Документ.Заполнение Цикл
		Если НЕ Строка.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Структура = кзоСКД.НоваяСтруктураОтбора(Тип("ЭлементОтбораКомпоновкиДанных"));
		Структура.Использование = Истина;
		Структура.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Строка.ИмяРеквизита);
		Структура.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Структура.ПравоеЗначение = Строка.Значение;
		Структура.Применение = ТипПримененияОтбораКомпоновкиДанных.Элементы;
		Структура.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
		
		НастройкиПроверки.Добавить(Структура);
	КонецЦикла;
	
	Возврат НастройкиПроверки;
	
КонецФункции

#КонецОбласти